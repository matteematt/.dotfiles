" Enabling filetype support provides filetype-specific indenting,
" syntax highlighting, omni-completion and other useful settings.
filetype plugin indent on
syntax on

" keybindings
let mapleader="\<Space>"

" `matchit.vim` is built-in so let's enable it!
" Hit `%` on `if` to jump to `else`.
runtime macros/matchit.vim

" Get the OS
if !exists("g:os")
  if has("win64") || has("win32") || has("win16")
    let g:os = "Windows"
  else
    let g:os = substitute(system('uname'), '\n', '', '')
  endif
endif

" Set different cursors for different modes
if g:os == "Darwin"
  " MacOS
  "Mode Settings
  let &t_SI.="\e[5 q" "SI = INSERT mode
  let &t_SR.="\e[4 q" "SR = REPLACE mode
  let &t_EI.="\e[1 q" "EI = NORMAL mode (ELSE)
  " Copy to and from system clipboard
  vnoremap <leader>yy :w !pbcopy<CR>
  nnoremap <leader>yp :r !pbpaste<CR>
elseif g:os == "Linux"
  let &t_SI = "\<Esc>[6 q"
  let &t_SR = "\<Esc>[4 q"
  let &t_EI = "\<Esc>[2 q"
  " Copy to and from system clipboard
  vnoremap <leader>yy :w !xclip -i -sel c<CR>
  nnoremap <leader>yp :r !xclip -o -sel -c<CR>
endif

" Colour scheme
if (has("termguicolors"))
	set termguicolors
	let &t_8f="\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b="\<Esc>[48;2;%lu;%lu;%lum"
endif

function! s:setup_gruvbit_colourscheme() abort
    hi Comment gui=italic cterm=italic
    hi Statement gui=bold cterm=bold
    hi VertSplit guibg=NONE ctermbg=NONE
endfunc
augroup colorscheme_change | au!
    au ColorScheme * call s:setup_gruvbit_colourscheme()
augroup END
colorscheme gruvbit

" various settings
set autoindent                 " Minimal automatic indenting for any filetype.
set smartindent 		            " smart indent for better indenting
set backspace=indent,eol,start " Proper backspace behavior.
set hidden                     " Possibility to have more than one unsaved buffers.
set incsearch                  " Incremental search, hit `<CR>` to stop.
set ruler                      " Shows the current line number at the bottom-right
                               " of the screen.
set wildmenu                   " Great command-line completion, use `<Tab>` to move
set wildmode=longest:full,full
                               " around and `<CR>` to validate
set wildcharm=<C-z>
set number	                   " shows the absolute number for the current line
set shiftwidth=2								" sets the shift tab and indent width to 2
set tabstop=2
set softtabstop=2
set gdefault										" global flag is on by default in searches, use /g to turn it off
set lazyredraw									" don't redraw when executing a macro
set ttimeoutlen=50
set shortmess+=A								" disable swap file prompt
set ignorecase									" together ignorecase and smartcase ignore case until an uppcase
set smartcase										" is specified, and then use case sensitivity
set shiftround									" rounds indents such as with >> to a tabstop
if &history < 1000
	set history=1000
endif
set spell
set spelllang=en
set foldmethod=indent
set foldlevelstart=2
set errorformat=%A%f:%l:%c:%m,%-G\\s%#,%-G%*\\d\ problem%.%#
set fillchars=fold:\ ,vert:â”‚    " char between panels
set matchpairs+=<:>             " add <> for matching pairs with %
let g:startup_section_len = 10     " set the number of recent items to show on my custom startup screen
let g:markdown_fenced_languages = ["html","python","vim","scala"]

" Create a new autogroup for all vimrc autocmds
augroup vimrc
  autocmd!
augroup END

" Can specify a bunch of keywords per language to add to the autocomplete menu
function! TryLoadLanguageDict() abort
  let fp = expand($HOME . "/.dotfiles/.vim/resources/syntaxdictionaries/" . &filetype . ".txt")
  if filereadable(fp)
    execute "setlocal complete+=k" . fp
    setlocal iskeyword+=-
  endif
endfunction
autocmd vimrc Filetype * :call TryLoadLanguageDict()

" You must create the dir set, it will not do it for you!
if has('persistent_undo')      "check if your vim version supports it
  set undodir=$HOME/.cache/vim/undo/  "directory where the undo files will be stored
  set undofile                 "turn on the feature
  silent call system('mkdir -p ' . &undodir)
endif

" Set custom fold line text
function! MyFoldText()
    let line = getline(v:foldstart)
    let foldedlinecount = v:foldend - v:foldstart + 1
    return ' * '. foldedlinecount . line
endfunction
set foldtext=MyFoldText()

" Turn off comments automatically continuing onto the next line
autocmd vimrc FileType * setlocal formatoptions-=c formatoptions-=r formatoptions-=o

" This autocommand tells Vim to open the quickfix window whenever a quickfix command is executed
" AND there are valid errors to display. And the list should close once it is empty
autocmd vimrc QuickFixCmdPost [^l]* cwindow
" Do same for location list
autocmd vimrc QuickFixCmdPost    l* lwindow
" Don't show spelling errors in the quickfix window (and location list)
autocmd vimrc FileType qf setlocal nospell

" Filetype detect
autocmd vimrc BufNewFile,BufRead *.scala  setfiletype scala

" Don't show spelling errors or line numbers when in terminal normal mode
autocmd vimrc TerminalOpen * setlocal nospell
autocmd vimrc TerminalOpen * setlocal nonumber

" Open split and get focus, remap the window movement keys
nnoremap <leader>w <C-w>v<C-w>l

" Use arrow keys for moving through the location and quickfix windows
nnoremap <Down> :cnext<CR>zz
nnoremap <Up> :cprev<CR>zz
nnoremap <Left> :lprev<CR>zz
nnoremap <Right> :lnext<CR>zz

" Highlight the last inserted text
nnoremap gV `[v`]`

" Open the undotree buffer
nnoremap <leader>u :UndotreeToggle<CR>

" :s// on visual selection
vnoremap <C-r> :s/\%V

" Open the current file location in netrw
nnoremap <leader>e :edit %:p:h<CR>

" Check what binaries are installed
let g:has_rg = executable("rg")
let g:has_git = executable("git")
let g:has_bat = executable("bat")
let g:has_ctags = executable("ctags")
let g:has_fzf = executable("fzf")

" Use these and then call :profile pause to find out why slowdown occurs
"profile start profile.log
"profile func *
"profile file *
